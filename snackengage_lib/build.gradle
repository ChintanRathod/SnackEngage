apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'

def version_name = '0.8'
def version_code = 8

android {
    compileSdkVersion 25
    buildToolsVersion "25"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode version_code
        versionName version_name
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:design:25.0.0'

    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile 'com.android.support:support-annotations:25.0.0'
    testCompile 'junit:junit:4.12'

    testCompile 'org.mockito:mockito-core:1.9.5'
}

apply plugin: 'maven'

def groupId = 'org.ligi'
def artifactId = 'snackengage'

def localReleaseDest = "${buildDir}/release/${version_name}"

task androidJavadocs(type: Javadoc) {
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version_name
        // Add other pom properties here if you want (developer details / licenses)
        repository(url: "file://${localReleaseDest}")
    }
}

task zipRelease(type: Zip) {
    from localReleaseDest
    destinationDir buildDir
    archiveName "release-${version_name}.zip"
}

task generateRelease << {
    println "Release ${version_name} can be found at ${localReleaseDest}/"
    println "Release ${version_name} zipped can be found ${buildDir}/release-${version_name}.zip"
}

generateRelease.dependsOn(uploadArchives)
generateRelease.dependsOn(zipRelease)

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
